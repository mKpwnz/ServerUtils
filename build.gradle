plugins {
    id 'java'
}

group = 'dev.mkpwnz'
version = '0.0.1'

repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}

dependencies {
    compileOnly("org.spigotmc:spigot-api:1.21.4-R0.1-SNAPSHOT")
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def devServerPluginsDir = localProperties.getProperty('minecraft.plugins.dir')

tasks.register("postBuild") {
    doLast {
        def pluginsDir = new File(devServerPluginsDir)

        pluginsDir.listFiles()?.each { file ->
            if (file.name.startsWith("${project.name}-") && file.name.endsWith(".jar")) {
                println "Delete old plugin Versions: ${file.name}"
                file.delete()
            }
        }

        copy {
            from "build/libs/${project.name}-${project.version}.jar"
            into pluginsDir
        }
        println "New Version ${project.name}-${project.version}.jar copied"
    }
}

tasks.named("build") {
    finalizedBy("postBuild")
}